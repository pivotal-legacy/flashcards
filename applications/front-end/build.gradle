import com.moowork.gradle.node.task.NpmTask

plugins {
    id "com.moowork.node" version "0.11"
}

node {
    // Version of node to use.
    version = '6.3.0'

    // Version of npm to use.
    npmVersion = '3.10.3'

    // Base URL for fetching node distributions (change if you have a mirror).
    distBaseUrl = 'https://nodejs.org/dist'

    // If true, it will download node using above parameters.
    // If false, it will try to use globally installed node.
    download = true

    // Set the work directory where node_modules should be located
    nodeModulesDir = file("${project.projectDir}")
}

configurations {
    compile
}

task zipBuild(type: Zip, dependsOn: ["build"]) {
    from "build"
    archiveName 'dist.zip'
    destinationDir projectDir
}

artifacts {
    compile zipBuild
}

buildDir = "build"

task clean(type: Delete) {
    delete "build"
}

task test(type: NpmTask) {
    args = ['test']
}

task build(dependsOn: test, type: NpmTask) {
    args = ['run', 'build']
}

build.dependsOn npmInstall
test.dependsOn npmInstall